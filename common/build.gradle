plugins {
    id 'maven-publish'
}

archivesBaseName = "${rootProject.mod_id}-common"

dependencies {
    // We depend on fabric loader here to use the fabric @Environment annotations and get the mixin dependencies
    // Do NOT use other classes from fabric loader
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury:${rootProject.architectury_version}"

    // Cloth Config
    modCompileOnly("me.shedaniel.cloth:cloth-config:${rootProject.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
}

architectury {
    common()
}

def mod_id = rootProject.mod_id
loom {
    accessWidenerPath = file("src/main/resources/${mod_id}.accesswidener")
}

def ENV = System.getenv()
publishing {
    publications {
        gpr(MavenPublication) {
            groupId = rootProject.group
            artifactId = "${rootProject.mod_id}-common"
            version = rootProject.mod_version
            from(components.java)
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/pssbletrngle/${rootProject.mod_id}")
            credentials {
                username = ENV.GITHUB_ACTOR
                password = ENV.GITHUB_TOKEN
            }
        }
    }
}